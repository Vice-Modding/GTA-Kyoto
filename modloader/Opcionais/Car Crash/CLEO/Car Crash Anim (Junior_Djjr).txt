{$CLEO}
wait 1000

04ED: load_animation  "carcrash_djjr"
while 84EE:   not animation "carcrash_djjr" loaded
wait 0
end

if and
0AF2: 12@ = get_float_from_ini_file "cleo\Car Crash Anim.ini" section "CFG" key "div"
0AF2: 13@ = get_float_from_ini_file "cleo\Car Crash Anim.ini" section "CFG" key "speedlimit"
0AF2: 14@ = get_float_from_ini_file "cleo\Car Crash Anim.ini" section "CFG" key "speedlimitdif"
then
else
    0ACD: show_text_highpriority "~r~Erro ao carregar o arquivo 'Car Crash Anim.ini'" time 5000
end

var
12@ : float
13@ : float
14@ : float
23@ : float
24@ : float
25@ : float
28@ : float
end

while true
    wait 0
    actor.Driving($player_actor)
    jf continue 
    0@ = actor.CurrentCar($player_actor)
    0441: 31@ = car 0@ model
    
    0A01:   model 31@ car
    jf continue
    
    
    0A97: 31@ = car 0@ struct   
    31@ += 0xD8 //m_fLastDamageIntensity
    
    0A97: 27@ = car 0@ struct
    27@ += 0x2A4 //20 = ré sem acelerar  19 = ré  65535 = nada
        
        
    while actor.Driving($player_actor)
        wait 0     
         
        0A8D: 26@ = read_memory 27@ size 4 virtual_protect 0  
        26@ == 65535
        jf continue
        
        056E:   car 0@ defined
        jf continue 
        
        02E3: 25@ = car 0@ speed
        25@ > 13@
        jf continue


        0A8D: 28@ = read_memory 31@ size 4 virtual_protect 0  
        
        if 28@ > 0.0
        then
            23@ = 25@
            23@ -= 24@
            
            if 23@ < 14@
            then
                28@ /= 12@
                if 28@ > 20.0
                then
                    28@ = 20.0
                end     
                

                if 0611:   actor $player_actor performing_animation "CAR_sit_pro"
                then 
                    if $ONMISSION == true
                    then 0A1A: actor $player_actor perform_walk_animation "car_crash_pro" IFP "carcrash_djjr" framedelta 28@ loop 0 lockX 0 lockY 0 lockF 0 time -1
                    else 0605: actor $player_actor perform_animation "car_crash_pro" IFP "carcrash_djjr" framedelta 28@ loop 0 lockX 0 lockY 0 lockF 0 time -1
                    end
                else   
                    if $ONMISSION == true
                    then 0A1A: actor $player_actor perform_walk_animation "car_crash_weak" IFP "carcrash_djjr" framedelta 28@ loop 0 lockX 0 lockY 0 lockF 0 time -1
                    else 0605: actor $player_actor perform_animation "car_crash_weak" IFP "carcrash_djjr" framedelta 28@ loop 0 lockX 0 lockY 0 lockF 0 time -1
                    end
                end                                                                                                                                       
                
                for 9@ = 0 to 2
                    if 8431:   not car 0@ passenger_seat_free 9@
                    then
                        0432: 11@ = get_actor_handle_from_car 0@ passenger_seat 9@
                        if 11@ > 0
                        then 
                            if $ONMISSION == true
                            then 0A1A: actor 11@ perform_walk_animation "car_crash_sit" IFP "carcrash_djjr" framedelta 28@ loop 0 lockX 0 lockY 0 lockF 0 time -1 
                            else 0605: actor 11@ perform_animation "car_crash_sit" IFP "carcrash_djjr" framedelta 28@ loop 0 lockX 0 lockY 0 lockF 0 time -1 
                            end
                        end 
                    end
                end
            end
        end 
        0087: 24@ = 25@
    end
end 
